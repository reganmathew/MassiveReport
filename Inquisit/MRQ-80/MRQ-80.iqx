<include>
/ file = "includes/instructions.iqx"
/ file = "includes/response_elements.iqx"
/ file = "includes/masks.iqx"
/ file = "includes/word_list.iqx"
/ file = "includes/absent_wordlist.iqx"
/ file = "includes/pictures.iqx"
/ file = "includes/stimulus_pairs.iqx"
</include>

*************************************************************************************

first, include all necessary dependencies above


next, define key values, trial types, and events

*************************************************************************************

<values>
/ wordcount = 0
/ imagecount = 0
/ totalwords = 80
/ totalimages = 10
/ imageName = "placeholder_image"
/ currentWord = "placeholder_descriptor"
</values>

<data>
/ columns=[subject, blockcode, trialcode, trialnum, latency, response, values.imageName, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber, stimulusitem, stimulusnumber]
</data>

<monkey>
/ latencydistribution = normal(400, 50)
/ percentcorrect = 80
</monkey>


<text progress>
/ items = ("Word <%values.wordcount%> of <%values.totalwords%>")
/ position = (50%, 87%)
/ size = (20%, 10%)
/ erase = false
</text>

<text imNum>
/ items = ("Image <%values.imagecount%> of <%values.totalimages%>")
/ position = (50%, 90%)
/ size = (20%, 10%)
/ erase = false
</text>


<trial practice>
/ ontrialbegin = [
	values.wordcount = 0;
	values.totalwords = 6;	
	values.imagecount +=1;
	values.totalimages = 3;
]
/ stimulustimes = [
	0 = focuspoint;
	1000 = practice_pics;
	1500 = mask1;
	1625 = mask2;
	1750 = mask3;
	1875 = mask4;
	2000 = mask5;
	]
/ timeout = 2000
/ branch = [
	trial.centre_mouse
]
</trial>

<trial test>
/ ontrialbegin = [
	values.wordcount = 0;
	values.imagecount +=1;
	values.totalwords = 80;
	values.totalimages = 10
]
/ stimulustimes = [
	0 = focuspoint;
	1000 = stimuli;
	1133 = mask1;	
	1250 = mask2;
	1375 = mask3;
	1500 = mask4;
	1650 = mask5;
	]
/ timeout = 2000
/ branch = [
	trial.centre_mouse
]
</trial>


*************************************************************************************

the centre_mouse trial fits between each picture and descriptor presentation


the purpose is to re-centre the mouse to avoid repetition bias

*************************************************************************************

<trial centre_mouse>
/ ontrialbegin = [
	if (block.practiceBlock)
		values.imageName = picture.practice_pics.currentitem
	elseif(block.testBlock)
		values.imageName = picture.stimuli.currentitem
]
/ stimulustimes = [
	0 = no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4, confidence, guessing, yes, no, progress, imNum, reveal;
]
/ inputdevice = mouse
/ validresponse = (reveal)
</trial>

*************************************************************************************

the present and absent words will be presented in a random order


these trials will be repeated until all word lists have been exhausted

*************************************************************************************

<trial practice_trial>
/ ontrialbegin = [
	values.wordcount += 1;
	values.currentWord = list.practice_words.currentvalue;
]
/ stimulusframes = [
	1 = no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4, confidence, guessing, yes, no, progress, imNum;
	2 = list.practice_words;
	]
/ inputdevice = mouse
/ validresponse = (no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4)
/ branch = [
	trial.centre_mouse
]
/ ontrialend = [
	if(values.wordcount == 5 && values.imagecount == 3)
	values.imagecount = 0;
]
</trial>

<trial present_trial>
/ ontrialbegin = [
	values.wordcount += 1;
	values.currentWord = list.present_words.currentvalue;
]
/ stimulusframes = [
	1 = no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4, confidence, guessing, yes, no, progress, imNum;
	2 = list.present_words;
	]
/ inputdevice = mouse
/ validresponse = (no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4)
/ correctresponse=(yes_3, yes_4)
/ branch = [
	trial.centre_mouse
]
</trial>

<trial absent_trial>
/ ontrialbegin = [
	values.wordcount += 1;
	values.currentWord = list.absent_words.currentvalue;
]
/ stimulusframes = [
	1 = no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4, confidence, guessing, yes, no, progress, imNum;
	2 = list.absent_words;
	]
/ inputdevice = mouse
/ validresponse = (no_1, no_2, no_3, no_4, yes_1, yes_2, yes_3, yes_4)
/ correctresponse=(no_3, no_4)
/ branch = [
	trial.centre_mouse
]
</trial>


*************************************************************************************

next, define the present and absent word list properties

*************************************************************************************

<list practice_words>
/ items = (text.p1, text.p2, text.p3)
/ selectionmode = picture.practice_pics.currentindex
/ selectionrate = block
</list>

<list present_words>
/ items = (text.p4, text.p5, text.p6, text.p7, text.p8, text.p9, text.p10, text.p11, text.p12, text.p13)
/ selectionmode = picture.stimuli.currentindex
/ selectionrate = block
</list>

<list absent_words>
/ items = (text.absent_words)
/ selectionmode = random
/ selectionrate = always
</list>


*************************************************************************************

note: the present words are yolked to specific image numbers with /selectionmode


now, we can simply present the stimuli in randomised order (1-picture, then 80-Qs)

*************************************************************************************

<block practiceBlock>
/ trials = [1=practice; 2-4=sequence(trial.practice_trial, trial.absent_trial)]
</block>

<block testBlock>
/ trials = [1=test; 2-81=noreplace(trial.present_trial, trial.absent_trial, trial.absent_trial)]
/ preinstructions = (task_reminder)
</block>

<expt>
/ blocks=[1-3=practiceBlock; 4-13=testBlock]
/ preinstructions = (block1_prepage)
</expt>